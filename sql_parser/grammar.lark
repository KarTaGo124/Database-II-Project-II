start: stmt (";" stmt)* ";"?

?stmt: create_table
     | load_data
     | create_index
     | drop_table
     | drop_index
     | alter_table
     | select_stmt
     | insert_stmt
     | delete_stmt

KEY: "KEY"
create_table: "CREATE" "TABLE" IDENT "(" coldef ("," coldef)* ")"
coldef: IDENT type_spec [KEY] ["INDEX" index_kind]

type_spec: "INT"                               -> t_int
         | "FLOAT"                             -> t_float
         | "DATE"                              -> t_date
         | "VARCHAR" "[" SIGNED_INT "]"        -> t_varchar
         | "ARRAY" "[" "FLOAT" "]"             -> t_array_2d
         | "ARRAY" "[" "FLOAT" "," SIGNED_INT "]" -> t_array_nd

INDEX_KIND: "SEQUENTIAL" | "ISAM" | "BTREE" | "RTREE" | "HASH" | "INVERTED_TEXT" | "MULTIMEDIA_SEQ" | "MULTIMEDIA_INV"
index_kind: INDEX_KIND
ident_or_string: IDENT | STRING

load_data: "LOAD" "DATA" "FROM" "FILE" STRING "INTO" IDENT ["WITH" "MAPPING" "(" column_mapping ("," column_mapping)* ")"]

column_mapping: IDENT "=" "ARRAY" "(" IDENT ("," IDENT)* ")"

create_index: "CREATE" "INDEX" "ON" IDENT ["(" IDENT ")"] "USING" index_kind ["LANGUAGE" STRING] ["FEATURE" STRING] ["DIRECTORY" STRING] ["PATTERN" STRING]

drop_table: "DROP" "TABLE" IDENT

drop_index: "DROP" "INDEX" IDENT "ON" IDENT

alter_table: "ALTER" "TABLE" IDENT "ADD" "COLUMN" IDENT "AS" "CONCAT" "(" col_list ")"
           | "ALTER" "TABLE" IDENT "DROP" "COLUMN" IDENT

select_all: "*"
select_cols: col_list
select_stmt: "SELECT" (select_all | select_cols) "FROM" IDENT ["WHERE" predicate] ["LIMIT" number]

?predicate: IDENT "=" literal                                    -> pred_eq
          | IDENT "BETWEEN" literal "AND" literal                -> pred_between
          | IDENT "IN" "(" spatial_point "," number ")"          -> pred_in
          | IDENT "NEAREST" "(" spatial_point "," SIGNED_INT ")" -> pred_nearest
          | IDENT "@@" STRING                                    -> pred_fulltext
          | IDENT "<->" STRING                                   -> pred_multimedia
          
insert_stmt: "INSERT" "INTO" IDENT ["(" col_list ")"] "VALUES" "(" literal ("," literal)* ")"
col_list: IDENT ("," IDENT)*

delete_stmt: "DELETE" "FROM" IDENT "WHERE" predicate

spatial_point: "(" number ("," number)* ")"
number: SIGNED_INT   -> int_lit
      | SIGNED_FLOAT -> float_lit

literal: number
       | STRING         -> string
       | "NULL"         -> null
       | spatial_point  -> array_lit

%import common.SIGNED_INT
%import common.SIGNED_FLOAT
%import common.CNAME         -> IDENT
%import common.ESCAPED_STRING-> STRING
%import common.WS
%ignore WS